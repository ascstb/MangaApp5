apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

androidExtensions {
    experimental = true
}

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.ascstb.mangaapp5"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "app_version_name", "${defaultConfig.versionName}"
        variant.resValue "string", "app_version", "${defaultConfig.versionName} - ${variant.versionCode}"
    }

    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-${gitBranch()}"
            proguardFiles getDefaultProguardFile('proguard-android.txt')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "mangaapp5.stage"
    productFlavors {
        dev {
            dimension "mangaapp5.stage"
            resValue "string", "app_name", "DEV - MangaApp 5"
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
            getProps("$projectDir.absolutePath/config/config.properties").each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
        production {
            dimension "mangaapp5.stage"
            resValue "string", "app_name", "MangaApp 5"
            applicationIdSuffix ""
            getProps("$projectDir.absolutePath/config/config.properties").each { p ->
                buildConfigField 'String', p.key, p.value
            }
        }
    }

    dataBinding {
        enabled = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation "androidx.navigation:navigation-fragment:$androidXUI"
    implementation "androidx.navigation:navigation-ui:$androidXUI"
    implementation "androidx.lifecycle:lifecycle-extensions:$androidXUI"
    implementation "androidx.navigation:navigation-fragment-ktx:$androidXUI"
    implementation "androidx.navigation:navigation-ui-ktx:$androidXUI"
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    kapt "com.android.databinding:compiler:$android_plugin_version"

    //Timber
    implementation "com.jakewharton.timber:timber:$timber"

    //RecyclerView & CardView
    implementation "com.android.support:recyclerview-v7:$recyclerview"
    implementation "com.android.support:cardview-v7:$recyclerview"
    implementation "com.android.support:gridlayout-v7:$recyclerview"
    implementation 'com.android.support:design:29.0.0'

    // ViewModel and LiveData
    implementation "android.arch.lifecycle:extensions:$lifecycle_version"

    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3"

    //Retrofit
    implementation "com.squareup.okhttp3:okhttp:$okhttp"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttp"
    implementation "com.squareup.retrofit2:retrofit:$retrofit"
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.squareup.retrofit2:converter-moshi:2.5.0'
    implementation "pl.droidsonroids.retrofit2:converter-jspoon:$jspoon"

    // Koin for Kotlin
    implementation "org.koin:koin-core:$koin"
    // Koin extended & experimental features
    implementation "org.koin:koin-core-ext:$koin"
    // AndroidX (based on koin-android)
    // Koin AndroidX Scope feature
    implementation "org.koin:koin-androidx-scope:$koin"
    // Koin AndroidX ViewModel feature
    implementation "org.koin:koin-androidx-viewmodel:$koin"

    //Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.9.0'

    //Room
    kapt "android.arch.persistence.room:compiler:$room"
    implementation "android.arch.persistence.room:runtime:$room"
    annotationProcessor "android.arch.persistence.room:compiler:$room"
}

static def gitBranch() {
    def branch = ""
    def proc = "git symbolic-ref -q --short HEAD".execute()
    proc.in.eachLine { line -> branch = line }
    proc.err.eachLine { line -> println line }
    proc.waitFor()
    if (branch == "") {
        branch = System.getenv('GIT_BRANCH')
    }
    if (branch == null) {
        branch = ""
    }
    def branchName = branch.replace("/", "_")
    branchName.replace("origin_", "")
}
